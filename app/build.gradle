plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs.kotlin"
    id "io.sentry.android.gradle" version "4.11.0"
    id "com.mikepenz.aboutlibraries.plugin"
    id "com.google.gms.google-services"
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.20'
    id "org.jetbrains.kotlin.plugin.compose" version "2.0.0"
}

sentry {
    autoInstallation {
        enabled = false
    }

    ignoredFlavors = ["foss"]
}

aboutLibraries {
    excludeFields = ["generated"]
}

android {
    def packageName = "de.hbch.traewelling"
    def name = "2.15.6"
    def code = 172
    compileSdk 35

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    defaultConfig {
        applicationId packageName
        minSdk 26
        targetSdk 35
        versionCode code
        versionName name

        manifestPlaceholders["appAuthRedirectScheme"] = "app.traewelldroid.de"

        testInstrumentationRunner
    }

    signingConfigs {
        release {
            try {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } catch (ignored) { }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            minifyEnabled false

            buildConfigField "String", "OAUTH_REDIRECT_URL", "\"https://app.traewelldroid.de/oauth2redirect\""
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"43\""
            buildConfigField "String", "REPO_URL", "\"https://github.com/Traewelldroid/traewelldroid\""
            buildConfigField "String", "PRIVACY_URL", "\"https://traewelldroid.de/privacy\""
            buildConfigField "String", "UNLEASH_URL", "\"https://unleash.traewelldroid.de/api/frontend\""
            buildConfigField "String", "UNLEASH_KEY", "\"default:production.286fca3aac1497f85ed886b3339c65cdfea9d5f52450524325398461\""
            buildConfigField "String", "WEBHOOK_URL", "\"https://webhook.traewelldroid.de\""
            buildConfigField "String", "UP_FCM_PROXY", "\"https://push.traewelldroid.de/FCM\""
            manifestPlaceholders["SENTRY_DSN"] = "https://6d3e121491134a8ea8ed3f560b03b5ec@bugs.traewelldroid.de/1"
            manifestPlaceholders["SENTRY_ENV"] = "production"

            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "String", "OAUTH_REDIRECT_URL", "\"https://app.traewelldroid.de/oauth2redirect\""
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"63\""
            buildConfigField "String", "REPO_URL", "\"https://github.com/Traewelldroid/traewelldroid\""
            buildConfigField "String", "PRIVACY_URL", "\"https://traewelldroid.de/privacy\""
            buildConfigField "String", "UNLEASH_URL", "\"https://unleash.traewelldroid.de/api/frontend\""
            buildConfigField "String", "UNLEASH_KEY", "\"default:development.52c54a43ebad9b9a668a69410b57cc19e44e19ab1ee40b4dd3f49b38\""
            buildConfigField "String", "WEBHOOK_URL", "\"https://webhooktest.traewelldroid.de\""
            buildConfigField "String", "UP_FCM_PROXY", "\"https://push.traewelldroid.de/FCM\""
            manifestPlaceholders["SENTRY_DSN"] = "https://6d3e121491134a8ea8ed3f560b03b5ec@bugs.traewelldroid.de/1"
            manifestPlaceholders["SENTRY_ENV"] = "development"
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        compose = true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace "de.hbch.traewelling"
    flavorDimensions = [ "libs" ]
    productFlavors {
        play {
            dimension "libs"
            versionNameSuffix "-play"
            apply plugin: "com.google.gms.google-services"
        }
        foss {
            dimension "libs"
            // versionNameSuffix "-foss"
        }
    }
}

aboutLibraries {
    configPath = "config"
}

dependencies {
    // Jetpack Compose
    def composeVersion = '1.7.3'
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:1.7.4"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation 'com.google.accompanist:accompanist-themeadapter-material3:0.36.0'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.36.0'
    implementation 'com.google.accompanist:accompanist-permissions:0.36.0'
    implementation "androidx.compose.material3:material3:1.3.0"
    implementation "androidx.activity:activity-compose:1.9.3"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.6"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.6"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.browser:browser:1.8.0"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "com.auth0.android:jwtdecode:2.0.2"
    implementation "net.openid:appauth:0.11.1"
    implementation "org.greenrobot:eventbus:3.3.1"

    // OSM integration
    implementation 'org.osmdroid:osmdroid-android:6.1.20'

    // Secure Storage
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation "com.github.jcloquell:android-secure-storage:0.1.3"

    // Navigation Component
    implementation "androidx.navigation:navigation-compose:2.8.3"

    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'

    // Coil for image loading
    implementation 'io.coil-kt:coil-compose:2.7.0'

    // Emoji pack support
    implementation 'de.c1710:filemojicompat-autoinit:3.2.7'

    // OSS licenses
    implementation 'com.mikepenz:aboutlibraries-compose:11.2.3'

    // Feature flags
    implementation 'io.getunleash:unleash-android-proxy-sdk:1.0.0'

    // Sentry
    playImplementation 'io.sentry:sentry-android:7.14.0'

    // Unified Push
    implementation 'com.github.UnifiedPush:android-connector:2.1.1'
    playImplementation('com.github.UnifiedPush:android-embedded_fcm_distributor:2.2.0') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    // CSV reader
    implementation 'com.github.doyaaaaaken:kotlin-csv-jvm:1.10.0'

    // Reviews
    playImplementation 'com.google.android.play:review:2.0.2'
    playImplementation 'com.google.android.play:review-ktx:2.0.2'

    // Showcase
    implementation 'com.canopas.intro-showcase-view:introshowcaseview:2.0.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3'
}


if (getGradle().getStartParameter().getTaskNames().stream().anyMatch {f -> f.contains("Play") }) {
    println("Play services will be applied!")
} else {
    println("Play services were not applied!")
}
